/*
 * Copyright (c) 2019 Telekom Deutschland AG
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'

apply from: "../gradle/task/quality.gradle"
apply from: "../gradle/task/code_coverage.gradle"
apply from: "../gradle/task/maven_push_library.gradle"

android {

    compileSdkVersion rootProject.ext.androidCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode rootProject.ext.androidVersionCode
        versionName rootProject.ext.androidVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/non-sensitive-repo-schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    libraryVariants.all { variant ->
        if (variant.buildType.name == "debug") {
            def aptOutputDir = new File(buildDir, "generated/source/apt/${variant.unitTestVariant.dirName}")
            variant.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/non-sensitive-repo-schemas".toString())
    }
}

dependencies {

    implementation "android.arch.persistence.room:runtime:1.1.1"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.android.support:appcompat-v7:28.0.0"

    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"

    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation "android.arch.persistence.room:testing:1.1.1"
    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"

    testImplementation "junit:junit:4.13"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
    testImplementation "org.json:json:20190722"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.powermock:powermock-module-junit4:1.6.6"
    testImplementation "org.powermock:powermock-api-mockito2:1.6.6"

    api project(':core')
}
