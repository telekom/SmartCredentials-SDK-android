/*
 * Copyright (c) 2019 Telekom Deutschland AG
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.util.regex.Matcher
import java.util.regex.Pattern

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
apply from: "../gradle/task/auto-version.gradle"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
    include("**")
}

publishing {
    group = "de.telekom.smartcredentials"
    version = getVersionName
    publications {
        aar(MavenPublication) {
            groupId project.getGroup()
            version = project.getVersion()
            artifactId project.getName()

            // Tell maven to prepare the generated "*.aar" file for publishing
            String aarFileName = "$buildDir/outputs/aar/${project.getName()}-release.aar"
            if (new File(aarFileName).exists()) {
                artifact(aarFileName)
                artifact sourcesJar
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                    if (getCurrentFlavor() == 'debug') {
                        configurations.debugImplementation.allDependencies.each {
                            if (it.name != 'unspecified') {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                    if (getCurrentFlavor() == 'release') {
                        configurations.releaseImplementation.allDependencies.each {
                            if (it.name != 'unspecified') {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = "https://dol.telekom.de/artifactory"
    String deployerUsername = System.getenv('DEVOPSLAB_DEPLOYER_USERNAME')
    String deployerPassword = System.getenv('DEVOPSLAB_DEPLOYER_PASSWORD')
    publish {
        repository {
            // The Artifactory repository key to publish to
            repoKey = 'smartcredentials-test-maven'

            // The username and password used to publish the sdk; set them in environment variables
            username = deployerUsername
            password = deployerPassword
        }
        defaults {
            // Tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publications('aar')
            publishArtifacts = true

            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

def getCurrentFlavor() {
    String tskReqStr = getGradle().getStartParameter().getTaskRequests().toString()

    Pattern pattern

    if (tskReqStr.contains("assemble"))
        pattern = Pattern.compile("assemble(\\w+)")
    else
        pattern = Pattern.compile("generate(\\w+)")

    Matcher matcher = pattern.matcher(tskReqStr)

    if (matcher.find())
        return matcher.group(1).toLowerCase()
    else {
        println "NO MATCH FOUND"
        return ""
    }
}